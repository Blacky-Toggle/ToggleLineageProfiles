apply plugin: 'com.android.application'

def sourceProfileIconList = new File(buildDir, "generated/source/lists")

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId "com.saiho.togglelineageprofiles"
        minSdkVersion 25
        targetSdkVersion 30
        versionCode 17
        versionName '1.0.17'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java {
                srcDirs += sourceProfileIconList
            }
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation files('libs/lineage-sdk.jar')
}

task generateProfileIconList {
    doLast {
        println "Generating ProfileIconList.java"
        def content = "package com.saiho.togglelineageprofiles;\n"
        content += "public final class ProfileIconList {\n"
        content += "public static java.util.HashMap<String, Integer> ids = new java.util.HashMap<>();\n"
        content += "static {\n"
        file("\\src\\main\\res\\drawable-nodpi").listFiles(new FilenameFilter() {
            @Override
            boolean accept(File file, String name) {
                return name.startsWith("profile_icon_") && name.endsWith(".png")
            }
        }).each {
            def fileName = it.name[0..it.name.length() - 5]
            content += "ids.put(\"" + fileName + "\", R.drawable." + fileName + ");\n"
        }
        content += "};\n"
        content += "}"

        def dir = new File(sourceProfileIconList, "com\\saiho\\togglelineageprofiles")
        dir.mkdirs()
        new File(dir, "ProfileIconList.java").text = content
    }
}
preBuild.dependsOn generateProfileIconList
